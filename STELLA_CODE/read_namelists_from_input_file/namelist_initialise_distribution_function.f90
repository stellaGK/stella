!###############################################################################
!####################### READ STELLA NAMELISTS FOR FIELDS ######################
!###############################################################################
! 
! This module will read the namelists associated with the fields:
! 
!   initialise_distribution
!      initialise_distribution_option = 'default'
!      phiinit = 1.0
!      scale_to_phiinit = .false.
! 
!   initialise_distribution_maxwellian
!      width0 = -3.5
!      den0 = 1.
!      upar0 = 0.
!      oddparity = .false.
!      chop_side = .false.
!      left = .true.
! 
!   initialise_distribution_noise
!      zf_init = 1.0
!      chop_side = .false.
!      left = .true.
!      rng_seed = -1
! 
!   initialise_distribution_kpar
!      width0 = -3.5
!      refac = 1.
!      imfac = 0.
!      den0 = 1.
!      upar0 = 0.
!      tpar0 = 0.
!      tperp0 = 0.
!      den1 = 0.
!      upar1 = 0.
!      tpar1 = 0.
!      tperp1 = 0.
!      den2 = 0.
!      upar2 = 0.
!      tpar2 = 0.
!      tperp2 = 0.
!      chop_side = .false.
!      left = .true.
! 
!   initialise_distribution_rh
!      kxmax = 1e+100
!      kxmin = 0.0
!      imfac = 0.0
!      refac = 1.0
! 
!   restart_options
!      tstart = 0.0
!      scale = 1.0
!      restart_file = trim(run_name)//'.nc'
!      restart_dir = './'
!      save_many = .true.
! 
! Text options for <initialise_distribution_option>:
!    - Maxwellian: {default, maxwellian}
!    - Noise generated by a random number generator (rng): {noise}
!    - Restarted simulation: {many, restart}
!    - kpar: {kpar}
!    - rh: {rh}
! 
! For each namelists two (or three) routines exist:
!    - set_default_parameters_<namelist>
!    - read_namelist_<namelist>
!    - check_inputs_<namelist>
! 
! First the default input parameters are set, then the default options are
! overwritten with those specified in the input file. Optionally, it is 
! checked whether any input variables are clashing with each other.
! 
!###############################################################################
module namelist_initialise_distribution_function

   implicit none

   ! Make reading routines accesible to other modules
   public :: read_namelist_initialise_distribution
   public :: read_namelist_initialise_distribution_maxwellian
   public :: read_namelist_initialise_distribution_noise
   public :: read_namelist_initialise_distribution_kpar
   public :: read_namelist_initialise_distribution_rh
   public :: read_namelist_restart_options
   
   ! Parameters need to be public
   public :: init_distribution_option_maxwellian, init_distribution_option_noise, init_distribution_option_restart
   public :: init_distribution_option_kpar, init_distribution_option_rh, init_distribution_option_remap
   
   private
   
   ! Create parameters for <init_distribution_option>
   integer, parameter :: init_distribution_option_maxwellian = 1
   integer, parameter :: init_distribution_option_noise = 2
   integer, parameter :: init_distribution_option_restart = 3
   integer, parameter :: init_distribution_option_kpar = 4
   integer, parameter :: init_distribution_option_rh = 5
   integer, parameter :: init_distribution_option_remap = 6 
   
   ! These variables are used in every single subroutine, so make them global
   integer :: in_file
   logical :: dexist

contains
   
   !****************************************************************************
   !                   INITIALISE POTENTIAL: READ THE SWITCH                   !
   !****************************************************************************
   subroutine read_namelist_initialise_distribution(init_distribution_switch, phiinit, scale_to_phiinit)

      use mp, only: proc0

      implicit none
      
      ! Variables that are read from the input file
      real, intent(out) :: phiinit
      logical, intent(out) :: scale_to_phiinit
      integer, intent(out) :: init_distribution_switch
      
      ! Local variable to set <init_distribution_switch>
      character(20) :: initialise_distribution_option
      
      !-------------------------------------------------------------------------

      if (.not. proc0) return
      call set_default_parameters_initialise_distribution
      call read_input_file_initialise_distribution
      call write_parameters_to_input_file

   contains
      
      !------------------------ Default input parameters -----------------------
      subroutine set_default_parameters_initialise_distribution

         implicit none

         ! Initial size of the potential
         phiinit = 1.0
         scale_to_phiinit = .false.
         
         ! Text options: {default, maxwellian, noise, many, restart, kpar, rh, remap}
         initialise_distribution_option = 'maxwellian'

      end subroutine set_default_parameters_initialise_distribution

      !---------------------------- Read input file ----------------------------
      subroutine read_input_file_initialise_distribution

         use file_utils, only: input_unit_exist
         use file_units, only: unit_error_file
         use text_options, only: text_option, get_option_value

         implicit none
         
         ! Link text options for <initialise_distribution_option> to an integer value
         type(text_option), dimension(8), parameter :: init_distribution_options = &
             (/text_option('default', init_distribution_option_maxwellian), &
               text_option('maxwellian', init_distribution_option_maxwellian), &
               text_option('noise', init_distribution_option_noise), &
               text_option('many', init_distribution_option_restart), &
               text_option('restart', init_distribution_option_restart), &
               text_option('kpar', init_distribution_option_kpar), &
               text_option('rh', init_distribution_option_rh), &
               text_option('remap', init_distribution_option_remap)/)

         ! Variables in the <initialise_distribution> namelist
         namelist /initialise_distribution/ initialise_distribution_option, phiinit, scale_to_phiinit
         
         !----------------------------------------------------------------------

         ! Overwrite the default input parameters by those specified in the input file
         in_file = input_unit_exist('initialise_distribution', dexist)
         if (dexist) read (unit=in_file, nml=initialise_distribution)
         
         ! Read the text option in <initialise_distribution> and store it in <init_distribution_switch>
         call get_option_value(initialise_distribution_option, init_distribution_options, init_distribution_switch, &
            unit_error_file, 'initialise_distribution_option in initialise_distribution')

      end subroutine read_input_file_initialise_distribution
      
      !------------------------- Write input parameters ------------------------
      subroutine write_parameters_to_input_file

         use file_units, only: unit => unit_input_file_with_defaults

         implicit none

         !-------------------------------------------------------------------------

         write (unit, '(A)') '&initialise_distribution'
         write (unit, '(A, A, A)') '  initialise_distribution_option = "', trim(initialise_distribution_option),'"'
         write (unit, '(A, ES0.4)') '  phiinit = ', phiinit
         write (unit, '(A, L0)') '  scale_to_phiinit = ', scale_to_phiinit
         write (unit, '(A)') '/'
         write (unit, '(A)') ''
      
      end subroutine write_parameters_to_input_file

   end subroutine read_namelist_initialise_distribution
   
   !****************************************************************************
   !                      INITIALISE POTENTIAL: MAXWELLIAN                     !
   !****************************************************************************
   subroutine read_namelist_initialise_distribution_maxwellian(width0, den0, upar0, oddparity, left, chop_side, zf_init)

      use mp, only: proc0
      
      implicit none
      
      ! Variables that are read from the input file
      real, intent(out) :: width0, den0, upar0
      logical, intent(out) :: oddparity
      logical, intent(out) :: left
      logical, intent(out) :: chop_side
      real, intent (out) :: zf_init
      
      !-------------------------------------------------------------------------

      if (.not. proc0) return
      call set_default_parameters_initialise_distribution_maxwellian
      call read_input_file_initialise_distribution_maxwellian
      call write_parameters_to_input_file

   contains
      
      !------------------------ Default input parameters -----------------------
      subroutine set_default_parameters_initialise_distribution_maxwellian

         implicit none
         
         width0 = -3.5
         den0 = 1.
         upar0 = 0.
         oddparity = .false.
         chop_side = .false.
         left = .true.
         zf_init = 1.0
      end subroutine set_default_parameters_initialise_distribution_maxwellian

      !---------------------------- Read input file ----------------------------
      subroutine read_input_file_initialise_distribution_maxwellian

         use file_utils, only: input_unit_exist
         
         implicit none

         namelist /initialise_distribution_maxwellian/ width0, den0, upar0, oddparity, left, chop_side, zf_init
         in_file = input_unit_exist('initialise_distribution_maxwellian', dexist)
         if (dexist) read (unit=in_file, nml=initialise_distribution_maxwellian) 

      end subroutine read_input_file_initialise_distribution_maxwellian
     
      !------------------------- Write input parameters ------------------------
      subroutine write_parameters_to_input_file

         use file_units, only: unit => unit_input_file_with_defaults

         implicit none

         !-------------------------------------------------------------------------

         write (unit, '(A)') '&initialise_distribution_maxwellian'
         write (unit, '(A, ES0.4)') '  width0 = ', width0
         write (unit, '(A, ES0.4)') '  den0 = ', den0
         write (unit, '(A, ES0.4)') '  upar0 = ', upar0
         write (unit, '(A, L0)') '  oddparity = ', oddparity
         write (unit, '(A, L0)') '  left = ', left
         write (unit, '(A, L0)') '  chop_side = ', chop_side
         write (unit, '(A)') '/'
         write (unit, '(A)') ''
      
      end subroutine write_parameters_to_input_file

   end subroutine read_namelist_initialise_distribution_maxwellian
   
   !****************************************************************************
   !                       INITIALISE DISTRIBUTION: NOISE                      !
   !****************************************************************************
   subroutine read_namelist_initialise_distribution_noise(zf_init, left, chop_side)

      use mp, only: proc0

      implicit none
      
      ! Variables that are read from the input file
      real, intent(out) :: zf_init
      logical, intent(out) :: left
      logical, intent(out) :: chop_side
      
      ! The <rng_seed> is read in init_random_number_generator()
      ! Here we need to define it, but will not be used
      integer  :: rng_seed
      
      !-------------------------------------------------------------------------

      if (.not. proc0) return
      call set_default_parameters_initialise_distribution_noise
      call read_input_file_initialise_distribution_noise
      call write_parameters_to_input_file

   contains
      
      !------------------------ Default input parameters -----------------------
      subroutine set_default_parameters_initialise_distribution_noise

         implicit none
         
         zf_init = 1.0
         chop_side = .false.
         left = .true.
         rng_seed = -1  ! Default value for random number generator seed

      end subroutine set_default_parameters_initialise_distribution_noise

      !---------------------------- Read input file ----------------------------
      subroutine read_input_file_initialise_distribution_noise

         use file_utils, only: input_unit_exist
         implicit none

         namelist /initialise_distribution_noise/ zf_init, left, chop_side, rng_seed
         in_file = input_unit_exist('initialise_distribution_noise', dexist)
         if (dexist) read (unit=in_file, nml=initialise_distribution_noise) 

      end subroutine read_input_file_initialise_distribution_noise
     
      !------------------------- Write input parameters ------------------------
      subroutine write_parameters_to_input_file

         use file_units, only: unit => unit_input_file_with_defaults

         implicit none

         !-------------------------------------------------------------------------

         write (unit, '(A)') '&initialise_distribution_noise'
         write (unit, '(A, ES0.4)') '  zf_init = ', zf_init
         write (unit, '(A, I0)') '  rng_seed = ', rng_seed
         write (unit, '(A, L0)') '  left = ', left
         write (unit, '(A, L0)') '  chop_side = ', chop_side
         write (unit, '(A)') '/'
         write (unit, '(A)') ''
      
      end subroutine write_parameters_to_input_file

   end subroutine read_namelist_initialise_distribution_noise
   
   !****************************************************************************
   !                         INITIALISE POTENTIAL: KPAR                        !
   !****************************************************************************
   subroutine read_namelist_initialise_distribution_kpar(&
         width0, refac, imfac, den0, upar0, tpar0, tperp0, &
         den1, upar1, tpar1, tperp1, den2, upar2, tpar2, tperp2, left, chop_side)

      use mp, only: proc0

      implicit none
      
      ! Variables that are read from the input file
      real, intent(out) :: width0, imfac, refac
      real, intent(out) :: den0, upar0, tpar0, tperp0
      real, intent(out) :: den1, upar1, tpar1, tperp1
      real, intent(out) :: den2, upar2, tpar2, tperp2
      logical, intent(out) :: left
      logical, intent(out) :: chop_side
      
      !-------------------------------------------------------------------------

      if (.not. proc0) return
      call set_default_parameters_initialise_distribution_kpar
      call read_input_file_initialise_distribution_kpar
      call write_parameters_to_input_file

   contains
      
      !------------------------ Default input parameters -----------------------
      subroutine set_default_parameters_initialise_distribution_kpar

         implicit none
         
         width0 = -3.5
         refac = 1.
         imfac = 0.
         den0 = 1.
         upar0 = 0.
         tpar0 = 0.
         tperp0 = 0.
         den1 = 0.
         upar1 = 0.
         tpar1 = 0.
         tperp1 = 0.
         den2 = 0.
         upar2 = 0.
         tpar2 = 0.
         tperp2 = 0.
         chop_side = .false.
         left = .true.

      end subroutine set_default_parameters_initialise_distribution_kpar

      !---------------------------- Read input file ----------------------------
      subroutine read_input_file_initialise_distribution_kpar

         use file_utils, only: input_unit_exist
         implicit none

         namelist /initialise_distribution_kpar/ width0, refac, imfac, den0, upar0, &
            tpar0, tperp0, den1, upar1, tpar1, tperp1, den2, upar2, tpar2, tperp2, left, chop_side
         in_file = input_unit_exist('initialise_distribution_kpar', dexist)
         if (dexist) read (unit=in_file, nml=initialise_distribution_kpar) 

      end subroutine read_input_file_initialise_distribution_kpar
     
      !------------------------- Write input parameters ------------------------
      subroutine write_parameters_to_input_file

         use file_units, only: unit => unit_input_file_with_defaults

         implicit none

         !-------------------------------------------------------------------------

         write (unit, '(A)') '&initialise_distribution_kpar'
         write (unit, '(A, ES0.4)') '  width0 = ', width0
         write (unit, '(A, ES0.4)') '  imfac = ', imfac
         write (unit, '(A, ES0.4)') '  refac = ', refac
         write (unit, '(A, ES0.4)') '  den0 = ', den0
         write (unit, '(A, ES0.4)') '  upar0 = ', upar0
         write (unit, '(A, ES0.4)') '  tpar0 = ', tpar0
         write (unit, '(A, ES0.4)') '  tperp0 = ', tperp0
         write (unit, '(A, ES0.4)') '  den1 = ', den1
         write (unit, '(A, ES0.4)') '  upar1 = ', upar1
         write (unit, '(A, ES0.4)') '  tpar1 = ', tpar1
         write (unit, '(A, ES0.4)') '  tperp1 = ', tperp1
         write (unit, '(A, ES0.4)') '  den2 = ', den2
         write (unit, '(A, ES0.4)') '  upar2 = ', upar2
         write (unit, '(A, ES0.4)') '  tpar2 = ', tpar2
         write (unit, '(A, ES0.4)') '  tperp2 = ', tperp2
         write (unit, '(A, L0)') '  left = ', left
         write (unit, '(A, L0)') '  chop_side = ', chop_side
         write (unit, '(A)') '/'
         write (unit, '(A)') ''
      
      end subroutine write_parameters_to_input_file

   end subroutine read_namelist_initialise_distribution_kpar
   
   !****************************************************************************
   !                          INITIALISE POTENTIAL: RH                         !
   !****************************************************************************
   subroutine read_namelist_initialise_distribution_rh(kxmin, kxmax, imfac, refac, init_wstar_rh, bessel_rh)

      use mp, only: proc0

      implicit none
      
      ! Variables that are read from the input file
      real, intent(out) :: kxmax, kxmin
      real, intent(out) :: imfac, refac
      logical, intent (out) :: init_wstar_rh
      logical, intent (out) :: bessel_rh
      !-------------------------------------------------------------------------

      if (.not. proc0) return
      call set_default_parameters_initialise_distribution_rh
      call read_input_file_initialise_distribution_rh
      call write_parameters_to_input_file

   contains
      
      !------------------------ Default input parameters -----------------------
      subroutine set_default_parameters_initialise_distribution_rh

         implicit none
         
         kxmax = 1e+100
         kxmin = 0.0
         imfac = 0.0
         refac = 1.0
         init_wstar_rh = .false.
         bessel_rh = .false. 

      end subroutine set_default_parameters_initialise_distribution_rh

      !---------------------------- Read input file ----------------------------
      subroutine read_input_file_initialise_distribution_rh

         use file_utils, only: input_unit_exist
         implicit none

         namelist /initialise_distribution_rh/ kxmin, kxmax, imfac, refac, init_wstar_rh, bessel_rh
         in_file = input_unit_exist('initialise_distribution_rh', dexist)
         if (dexist) read (unit=in_file, nml=initialise_distribution_rh) 

      end subroutine read_input_file_initialise_distribution_rh
     
      !------------------------- Write input parameters ------------------------
      subroutine write_parameters_to_input_file

         use file_units, only: unit => unit_input_file_with_defaults

         implicit none

         !-------------------------------------------------------------------------

         write (unit, '(A)') '&initialise_distribution_rh'
         write (unit, '(A, ES0.4)') '  kxmin = ', kxmin
         write (unit, '(A, ES0.4)') '  kxmax = ', kxmax
         write (unit, '(A, ES0.4)') '  imfac = ', imfac
         write (unit, '(A, ES0.4)') '  refac = ', refac
         write (unit, '(A)') '/'
         write (unit, '(A)') ''
      
      end subroutine write_parameters_to_input_file

   end subroutine read_namelist_initialise_distribution_rh
   
   !****************************************************************************
   !                              RESTART OPTIONS                              !
   !****************************************************************************
   subroutine read_namelist_restart_options(tstart, scale, restart_file, restart_dir, save_many)

      use mp, only: proc0

      implicit none
       
      ! Variables that are read from the input file
      real, intent(out) :: tstart, scale
      logical, intent(out) :: save_many
      character(len=300), intent(inout) :: restart_file
      character(len=150), intent(inout) :: restart_dir
      
      !-------------------------------------------------------------------------

      if (.not. proc0) return
      call set_default_parameters_restart_options
      call read_input_file_restart_options
      call write_parameters_to_input_file

   contains
      
      !------------------------ Default input parameters -----------------------
      subroutine set_default_parameters_restart_options
      
         use file_utils, only: run_name

         implicit none
         
         tstart = 0.0
         scale = 1.0
         restart_file = trim(run_name)//'.nc'
         restart_dir = './'
         save_many = .true.
         
         ! Note that the <save_many> and <save_many> are initialised at the
         ! start of this module so that they are accessible to save_stella_for_restart.fpp

      end subroutine set_default_parameters_restart_options

      !---------------------------- Read input file ----------------------------
      subroutine read_input_file_restart_options

         use file_utils, only: input_unit_exist
         implicit none

         namelist /restart_options/ tstart, scale, restart_file, restart_dir, save_many
         in_file = input_unit_exist('restart_options', dexist)
         if (dexist) read (unit=in_file, nml=restart_options) 

      end subroutine read_input_file_restart_options
     
      !------------------------- Write input parameters ------------------------
      subroutine write_parameters_to_input_file

         use file_units, only: unit => unit_input_file_with_defaults

         implicit none

         !-------------------------------------------------------------------------

         write (unit, '(A)') '&restart_options'
         write (unit, '(A, A, A)') '  restart_file = "', trim(restart_file),'"'
         write (unit, '(A, A, A)') '  restart_dir = "', trim(restart_dir),'"'
         write (unit, '(A, ES0.4)') '  tstart = ', tstart
         write (unit, '(A, ES0.4)') '  scale = ', scale
         write (unit, '(A, L0)') '  save_many = ', save_many
         write (unit, '(A)') '/'
         write (unit, '(A)') ''
        
      end subroutine write_parameters_to_input_file

   end subroutine read_namelist_restart_options

end module namelist_initialise_distribution_function

         
